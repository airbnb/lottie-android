plugins {
  id 'com.android.test'
  id 'org.jetbrains.kotlin.android'
}

android {
  namespace 'com.airbnb.lottie.sample.compose.benchmark'
  compileSdk 33

  compileOptions {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_17.toString()
    freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
  }

  defaultConfig {
    minSdk 30
    targetSdk 33

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    release {
      debuggable = true
      signingConfig = debug.signingConfig
    }
  }

  targetProjectPath = ":sample-compose"
  experimentalProperties["android.experimental.self-instrumenting"] = true
}

dependencies {
  implementation AndroidX.test.ext.junit
  implementation AndroidX.test.espresso.core
  implementation AndroidX.test.uiAutomator
  implementation AndroidX.benchmark.macroJunit4
}

androidComponents {
  beforeVariants(selector().all()) {
    enabled = buildType == "release"
  }
}
