import org.ajoberstar.grgit.Grgit
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
  id 'net.ltgt.errorprone' apply false
  id "com.google.devtools.ksp" apply false
  id "org.ajoberstar.grgit" apply false
  id "com.android.library" apply false
  id "org.jetbrains.kotlin.jvm" apply false
  id "com.vanniktech.maven.publish" apply false
  id "org.jetbrains.dokka" apply false
}

allprojects {
  def compileJavaVersion = 17
  def targetJavaVersion = 17
  pluginManager.withPlugin("java") {
    java {
      toolchain {
        languageVersion.set(JavaLanguageVersion.of(compileJavaVersion))
      }
    }
    tasks.withType(JavaCompile).configureEach {
      options.release.set(targetJavaVersion)
    }
  }
  pluginManager.withPlugin("org.jetbrains.kotlin.android") {
    kotlin {
      jvmToolchain(compileJavaVersion)
    }
    tasks.withType(KotlinCompile).configureEach {
      kotlinOptions.jvmTarget = targetJavaVersion
    }
  }
}

ext {
  git = Grgit.open(currentDir: project.rootDir)
  gitSha = git.head().id
  gitBranch = git.branch.current().name
}

tasks.register('clean', Delete) {
  delete rootProject.buildDir
}
